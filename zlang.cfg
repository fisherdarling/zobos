###
# ZOBOS.CFG --- grammar for 2020 Compilers final project
# This is NOT an SLR grammar, it has two shift-reduce conflicts due
# to the  UNARY -> PLUS VALUE  rule.  We will use a static LR parsing
# table that is coded to favor the shift operation for both conflicts.
###
PROGRAM   -> STMTS $
STATEMENT -> BRACESTMTS
           | DECLLIST sc
           | ASSIGN sc
           | IF
           | IFELSE
           | WHILE
           | EMIT sc
STMTS     -> STMTS STATEMENT
           | lambda
BRACESTMTS -> lbrace STMTS rbrace

# declaring variables
DECLTYPE  -> const bool
           | bool
           | const int
           | int
           | string
           | const float
           | float
DECLID    -> id 
           | ASSIGN
DECLIDS   -> DECLID
           | DECLIDS comma DECLID
DECLLIST  -> DECLTYPE DECLIDS 

# assignment
ASSIGN    -> id assign EXPR 
           | id assign ASSIGN

# control structures
IF        -> if lparen BEXPR rparen STATEMENT
IFELSE    -> if lparen BEXPR rparen BRACESTMTS else STATEMENT
WHILE     -> while lparen BEXPR rparen STATEMENT

EXPR      -> BEXPR | AEXPR

# boolean expressions
BOOLS     -> lt | leq | eq | geq | gt
BEXPR     -> AEXPR BOOLS AEXPR

# arithmetic expressions
PLUS      -> plus | minus
TIMES     -> mult | div | mod
AEXPR     -> SUM
SUM       -> SUM PLUS PRODUCT
           | PRODUCT
PRODUCT   -> PRODUCT TIMES VALUE
           | VALUE
VALUE     -> lparen AEXPR rparen 
           | lparen BEXPR rparen
           | intval
           | floatval
           | stringval
           | id
           | UNARY
           | CAST
UNARY     -> PLUS VALUE
           | not VALUE
           | compl VALUE
CAST      -> bool lparen AEXPR rparen
           | int lparen AEXPR rparen
           | float lparen AEXPR rparen

# on  emit symtable, write the current symbol table to the 
# third command line argument provided to ZOBOS
EMIT      -> emit id AEXPR AEXPR
           | emit symtable

